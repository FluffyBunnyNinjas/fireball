buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.3.1"
    }
}

def siteUrl = "https://github.com/Raniz85/java-url-handler"
def gitUrl = "https://github.com/Raniz85/java-url-handler.git"
group = "se.raneland.fireball"

version = "1.0.0.${System.currentTimeMillis()}"

apply plugin: "java"
apply plugin: "maven-publish"
apply plugin: "com.jfrog.bintray"

ext.awsSDKVersion = "1.10.+"

repositories {
    // Central maven repository
    mavenCentral()
}

dependencies {
            // AWS SDK, we need the interfaces and stuff
    compile "com.amazonaws:aws-java-sdk-dynamodb:${awsSDKVersion}",
            files("libs/sqlite4java.jar", "libs/DynamoDBLocal-repack-2014-04-24.jar")
            // JUnit 4
    testCompile "junit:junit:4.11"
}

jar {
    FileTree zip = zipTree("libs/DynamoDBLocal-repack-2014-04-24.jar")
    from zip
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

publishing {
    publications {
        java(MavenPublication) {
            from components.java
            groupId project.group
            artifactId project.name
            version project.version
        }
    }
}

bintray {
    try {
        user = bintrayUser
        key = bintrayApiKey
    } catch(MissingPropertyException e) {
        // Ignore
    }

    publications = ["java"]
    configurations = ["archives"]
    pkg {
        repo = "maven"
        // it is the name that appears in bintray when logged
        name = project.name
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
        version {
            name = project.version
            released = new Date()
            vcsTag = version
        }
    }
} 

task wrapper(type: Wrapper) {
    gradleVersion = "2.6"
}
